" ===================================================================================
" functions for VIM
" ===================================================================================
" {

   " RENAME CURRENT FILE (thanks Gary Bernhardt)
   " {
        function! RenameFile()
            let old_name=expand('%')
            let new_name=input('New file name: ', expand('%'), 'file')
            if new_name != '' && new_name != old_name
                exec ':saveas ' . new_name
                exec ':silent !rm ' . old_name
                redraw!
            endif
        endfunction
        " map <Leader>n :call RenameFile()<cr>
    " }
   
   " Merge a tab into a split in the previous window
   " {
        function! MergeTabs()
            if tabpagenr() == 1
                return
            endif
            let bufferName=bufname("%")
            if tabpagenr("$") == tabpagenr()
                close!
            else
                close!
                tabprev
            endif
            split
            execute "buffer " . bufferName
        endfunction
        " nmap <C-W>u :call MergeTabs()<CR>
   " }

    " Make a dir if no exists
    " {
        function! MakeDirIfNoExists(path)
            if !isdirectory(expand(a:path))
                call mkdir(expand(a:path), "p")
            endif
        endfunction
    " }

    " Number the tabs.
    " {
        function! MyTabLine()
            let s = ''
            let t = tabpagenr()
            let i = 1

            while i <= tabpagenr('$')
                let buflist = tabpagebuflist(i)
                let winnr = tabpagewinnr(i)
                let curwinnr = tabpagewinnr(i,'$')

                let s .= (i == t ? '%#TabLineSel#' : '%#TabLine#')
                let s .= '%' . i . 'T'
                let s .= ' ' . i . ': '
                let file = bufname(buflist[winnr - 1])
                let file = fnamemodify(file, ':p:t')
                if file == ''
                    let file = '[No Name]'
                endif
                let s .= file
                let s .= (curwinnr > 1 ? ' (' . curwinnr .') ' : '')
                let s .= ' '
                let i = i + 1
            endwhile
            let s .= '%T%#TabLineFill#%='
            let s .= (tabpagenr('$') > 1 ? '%999XX' : 'X')
            return s
        endfunction
    " }

    function! SplitItems(type, ...)
        let c = input("Split on what chars? ", ", ")
        normal! `[v`]x
        let @@ = substitute(@@, c, '\n', 'g')
        set paste
        exe "normal! i\<cr>\<esc>"
        pu! "
    set nopaste
    endfunction
    nnoremap <leader>s :set opfunc=SplitItems<cr>g@

    function! JoinItems(type, ...)
        let c = input("Join with what chars? ", ", ")
        normal! `[v']d
        let @@ = substitute(@@, '\n', c, 'g')
        set paste
        exe "normal! P\<esc>"
        set nopaste
    endfunction
    nnoremap <leader>j :set opfunc=JoinItems<cr>g@

    " Remove white space from the file
    " {
        " command! RemoveWhiteSpaces :%s/\s\+$//
        " save it as well
        command! RemoveWhiteSpaces :%s/\s\+$// | :w
    " }

    " Convert tabs to 4 spaces
    " {
        " command! TabsTo4Spaces :%s/\t/    /g
        " save it as well
        command! TabsTo4Spaces :%s/\t/    /g | :w
    " }

    " Convert tabs to 2 spaces
    " {
        " command! TabsTo2Spaces :%s/\t/  /g
        " save it as well
        command! TabsTo2Spaces :%s/\t/  /g | :w
    " }
" }

